import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.0'
	id 'eclipse'
	id 'java'
}

repositories {
	mavenCentral()

	// Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// Anvil GUI
	maven {
		url "https://repo.codemc.io/repository/maven-snapshots/"
	}

	// ProtocolLib
	maven {
		url "https://repo.dmulloy2.net/repository/public/"
	}

	// Skript
	maven {
		url 'https://repo.skriptlang.org/releases'
	}

	// Jitpack for Sitrica
	maven {
		url 'https://jitpack.io'
	}

}

dependencies {

	// Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.700')

	// Spigot/Bukkit
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.19.3-R0.1-SNAPSHOT')

	// Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.6.3') {
		transitive = false
	}

	// ProtocolLib
	implementation (group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.8.0") {
		transitive = false
	}

	shadow fileTree(dir: 'libraries', include: '*.jar')

}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ["-source", "8", "-target", "8"]
}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ('lang') {
        include '**/*'
        into 'lang/'
    }
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveVersion = project.property("version")
	archiveBaseName = project.name
	classifier = ''
	dependencies {
		exclude(dependency('org.spigotmc:'))
		exclude(dependency('org.yaml:'))
		exclude(dependency('io.netty:'))
	}
}